buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:2.5.2"
        classpath "io.spring.gradle:dependency-management-plugin:1.0.11.RELEASE"
    }
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    group = 'com.larry.fc.finalproject'
    version = '0.0.1-SNAPSHOT'
    sourceCompatibility = '11'

    repositories {
        mavenCentral()
    }

    dependencies {
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.0'
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
        testImplementation('org.springframework.boot:spring-boot-starter-test') {
            exclude group: 'junit', module: 'junit'
        }
    }

    test {
        useJUnitPlatform()
    }
}

/**
 * calendar-api 과 calendar-batch 모두 calendar-core 의 기능들을 사용할 수 있도록 설정해주는 부분
 */
project(':calendar-api') {
    dependencies {
        implementation project(':calendar-core')
    }
}

project(':calendar-batch') {
    dependencies {
        implementation project(':calendar-core')
    }
}